package hashtag;
import java_cup.runtime.*;
import java.util.*;



//------------------------------------------------PARSER CODE-------------------------------------------------------
parser code {:
    public ArrayList<Node> AST = new ArrayList();
    public int errors = 4;
public void report_error(String message, Object info){
  StringBuilder m = new StringBuilder("Error: ");
  if(info instanceof java_cup.runtime.Symbol){
       java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol)info);
       if(s.left >= 0){
        m.append(" en la linea " + (s.left+1));
         if(s.right >=0)
           m.append(", columna " + (s.right+1));
      }
      m.append(" @ symbol: " + s);
    }
     m.append(" : " + message);
     GUI.console.setText(GUI.console.getText() + m.toString() + "\n");
}

public void report_fatal_error(String message, Object info){
   report_error(message, info);
   //System.exit(1);
}
:}

//------------------------------------------------TERMINALES-------------------------------------------------------

terminal   SUMA,MENOS,DIV,MULT,MAYOR,MENOR,MAYORIGUAL,MENORIGUAL,NOT,DIFERENTE,IGUAL,ASIGNACION,IDENTIFICADOR;
terminal   PARDER,PARIZQ,MOD,COMA,PUNTOCOMA,MAINBEGIN,DOSPUNTOS;
terminal   AND,OR,FOR,IF,ELSE,WHILE,BEGIN,END,SWITCH,CASE,DO,TRUE,FALSE,FUNCTION,BREAK,RETURN,PRINT;
terminal   NUMERO,REAL,CADENA,CARACTER,READSTRING,READCHAR,READINT,READDOUBLE,OTHER,VOID;
terminal Integer   INT;
terminal Double    DOUBLE;
terminal Char      CHAR;
terminal String    STRING;
terminal Boolean   BOOLEAN;


//------------------------------------------------NO-TERMINALES----------------------------------------------------
non terminal Main,BodyList,BodyPart,Type,Asignacion,AsigValor,Program,FunctionList,FunctionPart,Stmts;
non terminal Instruccion,Llamado,ExpresionComparacion,OperadorComparacion,OperadorRelacional,ExpresionIncDec,Incremento,Decremento;
non terminal Bloque,Loops,Conditional,ForLoop,WhileLoop,IfConditional,SwitchConditional,ExprFor,UnionExpresion,Switch,ExprSwitchList,ExprSwitchPart;
non terminal ExpresionBooleana,ExpresionAritmetica,Factor,Term,ConditionGroup,Contenido;
non terminal Print,LlamadoMetodos,Booleana,Parametros,PrintParam,Param,Valor,EndFunction,Cadena,Return;
non terminal VarDeclare, VarInit, VarDeclarationList, VarDeclarationPart,Functions;

//------------------------------------------------PRECEDENCIA--------------------------------------------------------

precedence left  SUMA,MENOS;
precedence left  MULT,DIV,MOD; 
precedence left  PARIZQ;
//leer usuario , llamado metodos

//------------------------------------------------GRAMATICA-------------------------------------------------------

start with Program;
Program                         ::= BEGIN Functions Main END
                                    |
                                    BEGIN Main END
                                    ;

Functions                       ::= FunctionList;

Main                            ::= MAINBEGIN Stmts;

Stmts                           ::= DO BodyList END
                                    |
                                    DO Stmts END
                                    |
                                    DO END
                                    ;

BodyList                        ::= BodyPart BodyList
                                    |
                                    BodyPart;

BodyPart                        ::= Instruccion PUNTOCOMA
                                    |
                                    Bloque
                                    |
                                    Asignacion PUNTOCOMA;

/*DECLARACION VARIABLES ASIGNACION VARIABLES */
Instruccion                     ::= VarDeclare
                                    |
                                    VarInit
                                    |
                                    Llamado
                                    ;

VarDeclare                      ::= Type VarDeclarationList;

VarDeclarationList              ::= VarDeclarationPart COMA VarDeclarationList
                                    |
                                    VarDeclarationPart;

VarDeclarationPart              ::= IDENTIFICADOR;

VarInit                         ::= VarDeclare ASIGNACION AsigValor;

Valor                           ::= NUMERO 
                                    |
                                    IDENTIFICADOR
                                    |
                                    REAL;

AsigValor                       ::= Cadena
                                    |
                                    CARACTER
                                    |
                                    Booleana
                                   ;
Booleana                        ::= TRUE
                                    |
                                    FALSE;

Cadena                          ::= CADENA
                                    |
                                    ExpresionAritmetica;
                         
Asignacion                      ::= IDENTIFICADOR ASIGNACION AsigValor
                                    |
                                    ExpresionIncDec;

Type                            ::= INT                 
                                    |
                                    DOUBLE              
                                    |
                                    CHAR
                                    |
                                    BOOLEAN
                                    |
                                    STRING ; 

/*LLAMADOS*/
Llamado                         ::= Print
                                    |
                                    LlamadoMetodos
                                    ;

LlamadoMetodos                  ::= IDENTIFICADOR PARIZQ Contenido PARDER
                                    |
                                    IDENTIFICADOR PARIZQ PARDER;

Contenido                       ::= AsigValor COMA Contenido
                                    |
                                    AsigValor;
/*
Contenido                       ::= SubContenido;

SubContenido                    ::= AsigValor T
                                    | ;

T                               ::= COMA AsigValor  T
                                    | ;
*/   
/*PARAMETROD*/                     
Parametros                      ::= Parametros COMA Param
                                    |
                                    Param
                                    | ;

Param                           ::= Type IDENTIFICADOR:id;

Print                           ::= PRINT PARIZQ PrintParam PARDER;

PrintParam                      ::= IDENTIFICADOR
                                    |
                                    CADENA
                                    |
                                    LlamadoMetodos
                                    |  ;

/*FUNCTION */
FunctionList                    ::= FunctionPart FunctionList
                                    |
                                    FunctionPart;

FunctionPart                    ::= FUNCTION VOID IDENTIFICADOR PARIZQ Parametros PARDER Stmts
                                    |
                                    FUNCTION Type IDENTIFICADOR PARIZQ Parametros PARDER EndFunction;

EndFunction                     ::= DO BodyList RETURN Return PUNTOCOMA END
                                    |
                                    DO RETURN Return PUNTOCOMA END;

Return                          ::= LlamadoMetodos
                                    |
                                    AsigValor;

/*BLOQUE*/
Bloque                          ::= Loops
                                    |
                                    Conditional;

Loops                           ::= ForLoop
                                    |
                                    WhileLoop ;

Conditional                     ::= IfConditional
                                    |
                                    SwitchConditional ;

ForLoop                         ::= FOR PARIZQ ExprFor PARDER Stmts;

ExprFor                         ::= INT Asignacion PUNTOCOMA ExpresionBooleana PUNTOCOMA ExpresionIncDec ;

WhileLoop                       ::= WHILE PARIZQ ExpresionBooleana PARDER Stmts;

IfConditional                   ::= IF ConditionGroup Stmts
                                    |
                                    IF ConditionGroup Stmts ELSE Stmts
                                    ;

ConditionGroup                  ::= PARIZQ ExpresionBooleana PARDER;

SwitchConditional               ::= SWITCH PARIZQ IDENTIFICADOR PARDER DO ExprSwitchList END
                                    |
                                    SWITCH PARIZQ Switch PARDER DO ExprSwitchList END
                                    ;

ExprSwitchList                  ::= ExprSwitchPart ExprSwitchList
                                    |
                                    ExprSwitchPart;

ExprSwitchPart                  ::= CASE Switch DOSPUNTOS Stmts BREAK PUNTOCOMA
                                    |
                                    OTHER DOSPUNTOS Stmts BREAK PUNTOCOMA;

Switch                          ::= NUMERO
                                    |
                                    CARACTER;
/*
ExpresionSwitch                 ::= CASE Switch DOSPUNTOS Stmts BREAK PUNTOCOMA ExpresionSwitch
                                    |
                                    OTHER DOSPUNTOS Stmts BREAK PUNTOCOMA;

*/

/*EXPRESIONES*/

ExpresionAritmetica             ::= ExpresionAritmetica SUMA Factor
                                    |
                                    ExpresionAritmetica MENOS Factor
                                    |
                                    Factor
                                    ;

Factor                          ::= Factor MULT Term
                                    |
                                    Factor DIV Term
                                    |
                                    Factor MOD Term
                                    |
                                    Term
                                    ;

Term                            ::= PARIZQ ExpresionAritmetica Term
                                    |
                                    Valor
                                    ;

ExpresionBooleana               ::= ExpresionComparacion UnionExpresion
                                    |
                                    ExpresionComparacion
                                    |
                                    Booleana
                                    ;

UnionExpresion                  ::= OperadorComparacion ExpresionComparacion UnionExpresion
                                    |
                                    OperadorComparacion ExpresionComparacion
                                    ;

ExpresionComparacion            ::= AsigValor OperadorRelacional AsigValor
                                    |
                                    NOT ExpresionComparacion;

OperadorRelacional              ::= MAYOR 
                                    |
                                    MENOR
                                    |
                                    MAYORIGUAL
                                    |
                                    MENORIGUAL
                                    |
                                    IGUAL
                                    |
                                    DIFERENTE
                                    ;
OperadorComparacion             ::= AND
                                    |
                                    OR
                                    ;
ExpresionIncDec                 ::= IDENTIFICADOR Incremento
                                    |
                                    IDENTIFICADOR Decremento;

Incremento                      ::= SUMA SUMA;
Decremento                      ::= MENOS MENOS;
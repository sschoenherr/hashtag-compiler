package hashtag;
import java_cup.runtime.*;
import java.util.*;



//------------------------------------------------PARSER CODE-------------------------------------------------------
parser code {:

    public ArrayList<Node> AST = new ArrayList();

    public void report_error(String message, Object info){
      StringBuilder m = new StringBuilder("Error");
      if(info instanceof java_cup.runtime.Symbol){
           java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol)info);
           if(s.left >= 0){
            m.append(" en la linea " + (s.left+1));
             if(s.right >=0)
               m.append(", columna " + (s.right+1));
          }
          m.append(" @ symbol: " + s);
        }
         m.append(" : " + message);
         Interfaz.console.setText(Interfaz.console.getText() + m.toString() + "\n");
    }

    public void report_fatal_error(String message, Object info){
       report_error(message, info);
       //System.exit(1);
    }
:}

//------------------------------------------------TERMINALES-------------------------------------------------------

terminal   SUMA,MENOS,DIV,MULT,MAYOR,MENOR,MAYORIGUAL,MENORIGUAL,NOT,DIFERENTE,IGUAL,ASIGNACION;
terminal   PARDER,PARIZQ,MOD,COMA,PUNTOCOMA,MAINBEGIN,DOSPUNTOS;
terminal   AND,OR,FOR,IF,ELSE,WHILE,BEGIN,END,SWITCH,CASE,DO,FUNCTION,BREAK,RETURN,PRINT;
terminal   INT,DOUBLE,CHAR,STRING,BOOLEAN,READSTRING,READCHAR,READINT,READDOUBLE,OTHER,VOID;
terminal Integer   NUMERO;
terminal Double    REAL;
terminal Character CARACTER;
terminal String    CADENA, IDENTIFICADOR;
terminal Boolean   FALSE, TRUE;


//------------------------------------------------NO-TERMINALES----------------------------------------------------
non terminal Node Main,BodyList,BodyPart,Type,Asignacion,AsigValor,Program,FunctionList,FunctionPart,Stmts;
non terminal Node Instruccion,Llamado,ExpresionComparacion,OperadorComparacion,OperadorRelacional,ExpresionIncDec,Incremento,Decremento;
non terminal Node Bloque,Loops,Conditional,ForLoop,WhileLoop,IfConditional,SwitchConditional,ExprFor,UnionExpresion,Switch,ExprSwitchList,ExprSwitchPart;
non terminal Node ExpresionBooleana,ExpresionAritmetica,Factor,Term,ConditionGroup,Contenido,PrintParam;
non terminal Node Print,LlamadoMetodos,Booleana,Parametros,Multiple,Valor,EndFunction,Cadena,Return;
non terminal Node VarDeclare, VarInit, VarDeclarationList, VarDeclarationPart,Functions;

//------------------------------------------------PRECEDENCIA--------------------------------------------------------

precedence left  SUMA,MENOS;
precedence left  MULT,DIV,MOD; 
precedence left  PARIZQ;
//leer usuario , llamado metodos

//------------------------------------------------GRAMATICA-------------------------------------------------------

start with Program;
Program                         ::= BEGIN Functions:f Main:mn END
                                    {: 
                                        RESULT = new Node("PROG", f, mn);
                                        parser.AST.add(RESULT);
                                    :}
                                    |
                                    BEGIN Main:mn END
                                    {: RESULT = new Node("PROG", mn);
                                       parser.AST.add(RESULT); :}
                                    ;

Functions                       ::= FunctionList:fl
                                    {:
                                        RESULT = new Node("FUNCTIONS",fl);
                                    :};

Main                            ::= MAINBEGIN Stmts:st
                                    {: RESULT = new Node("MAIN",st); :}
                                    ;

Stmts                           ::= DO BodyList:bdl END
                                    {:
                                        RESULT = bdl;
                                    :}
                                    |
                                    DO Stmts:st END
                                    {: RESULT = st; :}
                                    |
                                    DO END
                                    {: RESULT = new Node("λ"); :}
                                    ;

BodyList                        ::= BodyPart:bp BodyList:bdl
                                    {:
                                        RESULT = bdl.add(bp);
                                    :}
                                    |
                                    BodyPart:bp
                                    {: RESULT = new Node("body", bp); :}
                                    ;

BodyPart                        ::= Instruccion:ins PUNTOCOMA
                                    {: RESULT = ins; :}
                                    |
                                    Bloque:bq
                                    {: RESULT = bq; :}
                                    |
                                    Asignacion:asig PUNTOCOMA
                                    {: RESULT = asig; :}
                                    ;

/*DECLARACION VARIABLES ASIGNACION VARIABLES */

Instruccion                     ::= VarDeclare:vd
                                    {: RESULT = vd; :}
                                    |
                                    VarInit:vi
                                    {: RESULT = vi; :}
                                    |
                                    Llamado:l
                                    {: RESULT = l; :}
                                    ;

VarDeclare                      ::= Type VarDeclarationList:vdl
                                    {:
                                        Node node = new Node(vdl.label);
                                        RESULT = new Node("declare",vdl.getChildren()).add(node);
                                    :};

VarDeclarationList              ::= VarDeclarationPart:vdp COMA VarDeclarationList:vdl
                                    {: RESULT = vdl.add(vdp); :}
                                    |
                                    VarDeclarationPart:vdp
                                    {: RESULT = vdp; :};

VarDeclarationPart              ::= IDENTIFICADOR:id
                                    {: RESULT = new Node(id); :};

VarInit                         ::= VarDeclare:vd ASIGNACION AsigValor:av
                                    {: RESULT = new Node("assign",vd,av); :};

Valor                           ::= NUMERO:num
                                    {: RESULT = new Node(Integer.toString(num)); :}
                                    |
                                    IDENTIFICADOR:id
                                    {: RESULT = new Node(id); :}
                                    |
                                    REAL:r
                                    {: RESULT = new Node(Double.toString(r)); :}
                                    ;

AsigValor                       ::= Cadena:c
                                    {: RESULT = c; :}
                                    |
                                    CARACTER:ch
                                    {: RESULT = new Node("'"+Character.toString(ch)+"'"); :}
                                    |
                                    Booleana:bool
                                    {: RESULT = bool; :}
                                    ;

Booleana                        ::= TRUE
                                    {: RESULT = new Node("true"); :}
                                    |
                                    FALSE
                                    {: RESULT = new Node("false"); :}
                                    ;

Cadena                          ::= CADENA:cad
                                    {: RESULT = new Node("\""+cad+"\""); :}
                                    |
                                    ExpresionAritmetica:ea
                                    {: RESULT = ea; :}
                                    ;
                         
Asignacion                      ::= IDENTIFICADOR:id ASIGNACION AsigValor:av
                                    {: RESULT = new Node("assign",new Node(id), av); :}
                                    |
                                    ExpresionIncDec:update
                                    {: RESULT = update; :};

Type                            ::= INT               
                                    |
                                    DOUBLE              
                                    |
                                    CHAR
                                    |
                                    BOOLEAN
                                    |
                                    STRING ; 

/*LLAMADOS*/
Llamado                         ::= Print:pr
                                    {: RESULT = pr; :}
                                    |
                                    LlamadoMetodos:lm
                                    {: RESULT = lm; :}
                                    ;

LlamadoMetodos                  ::= IDENTIFICADOR:id PARIZQ Contenido:cont PARDER
                                    {: RESULT = new Node("function_call",new Node(id),cont); :}
                                    |
                                    IDENTIFICADOR:id PARIZQ PARDER
                                    {: RESULT = new Node("function_call", new Node(id), new Node("params",new Node("λ"))); :};

Contenido                       ::= AsigValor:av COMA Contenido:cont
                                    {: RESULT = cont.add(av); :}
                                    |
                                    AsigValor:av
                                    {: RESULT = new Node("params",av); :};
   
/*PARAMETROS*/                     
Parametros                      ::= Type IDENTIFICADOR:id Multiple:mult
                                    {: RESULT = new Node("params", new Node(id), mult); :}
                                    |
                                    Type IDENTIFICADOR:id
                                    {: RESULT = new Node("params", new Node(id)); :}
                                    |
                                    {: RESULT = new Node("params", new Node("λ")); :}
                                    ;

Multiple                        ::= COMA Parametros:param
                                    {: RESULT = param; :}
                                    ;

Print                           ::= PRINT PARIZQ PrintParam:pp PARDER
                                    {: RESULT = new Node("PRINT", pp); :};

PrintParam                      ::= IDENTIFICADOR:id
                                    {: RESULT = new Node(id); :}
                                    |
                                    CADENA:cad
                                    {: RESULT = new Node("\""+cad+"\""); :}
                                    |
                                    LlamadoMetodos:lm
                                    |  ;

/*FUNCTION */
FunctionList                    ::= FunctionPart:fp FunctionList:fl
                                    {: RESULT = fl.add(fp); :}
                                    |
                                    FunctionPart:fp
                                    {: RESULT = new Node("body",fp); :}
                                    ;

FunctionPart                    ::= FUNCTION VOID IDENTIFICADOR:id PARIZQ Parametros:param PARDER Stmts:st
                                    {: RESULT = new Node(id, param, st); :}
                                    |
                                    FUNCTION Type IDENTIFICADOR:id PARIZQ Parametros:param PARDER EndFunction:ef
                                    {: RESULT = new Node(id,param,ef); :}
                                    ;

EndFunction                     ::= DO BodyList:bdl RETURN Return:rt PUNTOCOMA END
                                    {: RESULT = new Node("branch", bdl, rt);  :}
                                    |
                                    DO RETURN Return:rt PUNTOCOMA END
                                    {: RESULT = rt; :}
                                    ;

Return                          ::= LlamadoMetodos:lm
                                    {: RESULT = new Node("RETURN",lm); :}
                                    |
                                    AsigValor:av
                                    {: RESULT = new Node("RETURN", av); :}
                                    ;

/*BLOQUE*/
Bloque                          ::= Loops:lp
                                    {: RESULT = lp; :}
                                    |
                                    Conditional:cond
                                    {: RESULT = cond; :}
                                    ;

Loops                           ::= ForLoop:flp
                                    {: RESULT = flp; :}
                                    |
                                    WhileLoop:wlp
                                    {: RESULT = wlp; :}
                                    ;

Conditional                     ::= IfConditional:ic
                                    {: RESULT = ic; :}
                                    |
                                    SwitchConditional:swc
                                    {: RESULT = swc; :};

ForLoop                         ::= FOR PARIZQ ExprFor:expr PARDER Stmts:st
                                    {: RESULT = new Node("FOR", expr, st); :};

ExprFor                         ::= INT Asignacion:asig PUNTOCOMA ExpresionBooleana:exprb PUNTOCOMA ExpresionIncDec:update
                                    {: RESULT = new Node("structure",asig,exprb,update); :};

WhileLoop                       ::= WHILE PARIZQ ExpresionBooleana:exprb PARDER Stmts:st
                                    {: RESULT = new Node("WHILE", exprb, st); :}
                                    ;

IfConditional                   ::= IF ConditionGroup:cg Stmts:st
                                    {: RESULT = new Node("IF",cg, st); :}
                                    |
                                    IF ConditionGroup:cg Stmts:st1 ELSE Stmts:st2
                                    {: RESULT = new Node("IF",cg,st1,new Node("ELSE",st2)); :};

ConditionGroup                  ::= PARIZQ ExpresionBooleana:eb PARDER
                                    {: RESULT = eb; :}
                                    |
                                    PARIZQ IDENTIFICADOR:id PARDER
                                    {: RESULT = new Node(id); :};

SwitchConditional               ::= SWITCH PARIZQ IDENTIFICADOR:id PARDER DO ExprSwitchList:exprl END
                                    {: RESULT = new Node("SWITCH", new Node(id), exprl); :}
                                    |
                                    SWITCH PARIZQ Switch:sw PARDER DO ExprSwitchList:exprl END
                                    {: RESULT = new Node("SWITCH", sw, exprl); :}
                                    ;

ExprSwitchList                  ::= ExprSwitchPart:exprp ExprSwitchList:exprl
                                    {: RESULT = exprl.add(exprp); :}
                                    |
                                    ExprSwitchPart:exprp
                                    {: RESULT = new Node("body",exprp); :};

ExprSwitchPart                  ::= CASE Switch:sw DOSPUNTOS Stmts:st BREAK PUNTOCOMA
                                    {: RESULT = new Node("CASE", sw, st); :}
                                    |
                                    OTHER DOSPUNTOS Stmts:st BREAK PUNTOCOMA
                                    {: RESULT = new Node("OTHER", st); :};

Switch                          ::= NUMERO:n
                                    {: RESULT = new Node(Integer.toString(n)); :}
                                    |
                                    CARACTER:c
                                    {: RESULT = new Node(Character.toString(c)); :};

/*EXPRESIONES*/

ExpresionAritmetica             ::= ExpresionAritmetica:ea SUMA Factor:fct
                                    {: RESULT = new Node("+",ea,fct); :}
                                    |
                                    ExpresionAritmetica:ea MENOS Factor:fct
                                    {: RESULT = new Node("-",ea,fct); :}
                                    |
                                    Factor:fct
                                    {: RESULT = fct; :}
                                    ;

Factor                          ::= Factor:fct MULT Term:trm
                                    {: RESULT = new Node("*",fct,trm); :}
                                    |
                                    Factor:fct DIV Term:trm
                                    {: RESULT = new Node("/",fct,trm); :}
                                    |
                                    Factor:fct MOD Term:trm
                                    {: RESULT = new Node("%",fct,trm); :}
                                    |
                                    Term:trm
                                    {: RESULT = trm; :}
                                    ;

Term                            ::= PARIZQ ExpresionAritmetica:ea PARDER
                                    {: RESULT = ea; :}
                                    |
                                    Valor:vlr
                                    {: RESULT = vlr; :}
                                    ;

ExpresionBooleana               ::= ExpresionComparacion:exprc UnionExpresion:uexpr
                                    {: RESULT = new Node("conditions",exprc,uexpr); :}
                                    |
                                    ExpresionComparacion:ec
                                    {: RESULT = ec; :}
                                    |
                                    Booleana:bool
                                    {: RESULT = bool; :}
                                    ;

UnionExpresion                  ::= OperadorComparacion:opcomp ExpresionComparacion:exprc UnionExpresion:uexpr
                                    {: RESULT = uexpr.add(opcomp,exprc); :}
                                    |
                                    OperadorComparacion:opcomp ExpresionComparacion:ec
                                    {: RESULT =  opcomp.add(ec);:}
                                    ;

ExpresionComparacion            ::= AsigValor:av1 OperadorRelacional:oprel AsigValor:av2
                                    {: RESULT = new Node(oprel.label, av1, av2); :}
                                    |
                                    NOT ExpresionComparacion:exprc
                                    {: RESULT = new Node("negation", exprc); :}
                                    ;

OperadorRelacional              ::= MAYOR
                                    {: RESULT = new Node(">"); :}
                                    |
                                    MENOR
                                    {: RESULT = new Node("<"); :}
                                    |
                                    MAYORIGUAL
                                    {: RESULT = new Node(">="); :}
                                    |
                                    MENORIGUAL
                                    {: RESULT = new Node("<="); :}
                                    |
                                    IGUAL
                                    {: RESULT = new Node("="); :}
                                    |
                                    DIFERENTE
                                    {: RESULT = new Node("!="); :}
                                    ;
OperadorComparacion             ::= AND
                                    {: RESULT = new Node("AND"); :}
                                    |
                                    OR
                                    {: RESULT = new Node("OR"); :}
                                    ;
ExpresionIncDec                 ::= IDENTIFICADOR:id Incremento
                                    {: RESULT = new Node("update ++", new Node(id)); :}
                                    |
                                    IDENTIFICADOR:id Decremento
                                    {: RESULT = new Node("update --", new Node(id)); :};

Incremento                      ::= SUMA SUMA;
Decremento                      ::= MENOS MENOS;
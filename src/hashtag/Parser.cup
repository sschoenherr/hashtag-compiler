package hashtag;
import java_cup.runtime.*;
import java.util.*;



//------------------------------------------------PARSER CODE-------------------------------------------------------
parser code {:

    public ArrayList<Node> AST = new ArrayList();

    public void report_error(String message, Object info){
      StringBuilder m = new StringBuilder("Error");
      if(info instanceof java_cup.runtime.Symbol){
           java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol)info);
           if(s.left >= 0){
            m.append(" en la linea " + (s.left+1));
             if(s.right >=0)
               m.append(", columna " + (s.right+1));
          }
          m.append(" @ symbol: " + s);
        }
         m.append(" : " + message);
         Interfaz.console.setText(Interfaz.console.getText() + m.toString() + "\n");
    }

    public void report_fatal_error(String message, Object info){
       report_error(message, info);
       //System.exit(1);
    }
:}

//------------------------------------------------TERMINALES-------------------------------------------------------

terminal   SUMA,MENOS,DIV,MULT,MAYOR,MENOR,MAYORIGUAL,MENORIGUAL,NOT,DIFERENTE,IGUAL,ASIGNACION;
terminal   PARDER,PARIZQ,MOD,COMA,PUNTOCOMA,MAINBEGIN,DOSPUNTOS;
terminal   AND,OR,FOR,IF,ELSE,WHILE,BEGIN,END,SWITCH,CASE,DO,FUNCTION,BREAK,RETURN,PRINT;
terminal   INT,DOUBLE,CHAR,STRING,BOOLEAN,READSTRING,READCHAR,READINT,READDOUBLE,OTHER,VOID;
terminal Integer   NUMERO;
terminal Double    REAL;
terminal Character CARACTER;
terminal String    CADENA, IDENTIFICADOR;
terminal Boolean   FALSE, TRUE;


//------------------------------------------------NO-TERMINALES----------------------------------------------------
non terminal Node Main,BodyList,BodyPart,DeclaracionVariable,DeclaracionSimple,DeclaracionMultiple,Type,Asignacion,AsigValor,Var,Program,FunctionList,FunctionPart,Stmts;
non terminal Node Instruccion,Llamado,Expresion,ExpresionComparacion,OperadorComparacion,OperadorRelacional,ExpresionIncDec,Incremento,Decremento;
non terminal Node Bloque,Loops,Conditional,ForLoop,WhileLoop,IfConditional,SwitchConditional,ExprFor,UnionExpresion,ExpresionSwitch,Switch;
non terminal Node ExpresionBooleana,ExpresionAritmetica,Factor,Term,ConditionGroup,T,SubContenido,Contenido;
non terminal Node Print,LlamadoMetodos,Booleana,Parametros,x,Multiple,Valor,EndFunction,Cadena,Return;

//------------------------------------------------PRECEDENCIA--------------------------------------------------------

precedence left  SUMA,MENOS;
precedence left  MULT,DIV,MOD; 
precedence left  PARIZQ;
//leer usuario , llamado metodos

//------------------------------------------------GRAMATICA-------------------------------------------------------

start with Program;
Program                         ::= BEGIN FunctionList:fl Main:mn END
                                    {: 
                                        RESULT = new Node("PROG", fl, mn);
                                        parser.AST.add(RESULT);
                                    :}
                                    |
                                    BEGIN Main:mn END
                                    {: RESULT = new Node("PROG", mn);
                                       parser.AST.add(RESULT); :}
                                    ;

Main                            ::= MAINBEGIN Stmts:st
                                    {: RESULT = new Node("MAIN", st.getChildren()); :}
                                    ;

Stmts                           ::= DO BodyList:bdl END
                                    {:
                                        RESULT = bdl;
                                    :}
                                    |
                                    DO Stmts:st END
                                    {: RESULT = st; :}
                                    |
                                    DO END
                                    {: RESULT = new Node("Î»"); :}
                                    ;

BodyList                        ::= BodyPart:bp BodyList:bdl
                                    {:
                                        RESULT = bdl.addChild(bp);
                                    :}
                                    |
                                    BodyPart:bp
                                    {: RESULT = new Node("body", bp); :}
                                    ;

BodyPart                        ::= Instruccion:ins PUNTOCOMA
                                    {: RESULT = ins; :}
                                    |
                                    Bloque:bq
                                    {: RESULT = bq; :}
                                    |
                                    Asignacion:asig PUNTOCOMA
                                    {: RESULT = asig; :}
                                    ;

/*DECLARACION VARIABLES ASIGNACION VARIABLES */

Instruccion                     ::= DeclaracionVariable:dv
                                    {: RESULT = dv; :}
                                    |
                                    Llamado:l
                                    {: RESULT = l; :}
                                    ;

DeclaracionVariable             ::= Type IDENTIFICADOR:id Var:vr
                                    {: RESULT = new Node("init",new Node(id),vr); :}
                                    ;

Var                             ::= DeclaracionSimple:ds
                                    {: RESULT = ds; :}
                                    |
                                    DeclaracionMultiple:dm
                                    {: RESULT = dm; :}
                                    ;

DeclaracionSimple               ::= ASIGNACION AsigValor:av
                                    {: RESULT = av; :}
                                    ;

DeclaracionMultiple             ::= COMA IDENTIFICADOR:id Var:vr
                                    {: RESULT = new Node("initialize",new Node(id),vr); :}
                                    |
                                    {: RESULT = new Node(""); :}
                                    ;

Valor                           ::= NUMERO:num
                                    {: RESULT = new Node(Integer.toString(num)); :}
                                    |
                                    IDENTIFICADOR:id
                                    {: RESULT = new Node(id); :}
                                    |
                                    REAL:r
                                    {: RESULT = new Node(Double.toString(r)); :}
                                    ;

AsigValor                       ::= Cadena:c
                                    {: RESULT = c; :}
                                    |
                                    CARACTER:ch
                                    {: RESULT = new Node(Character.toString(ch)); :}
                                    |
                                    Booleana:bool
                                    {: RESULT = bool; :}
                                    ;

Booleana                        ::= TRUE:tr
                                    {: RESULT = new Node(Boolean.toString(tr)); :}
                                    |
                                    FALSE:fls
                                    {: RESULT = new Node(Boolean.toString(fls)); :}
                                    ;

Cadena                          ::= CADENA:cad
                                    {: RESULT = new Node(cad); :}
                                    |
                                    ExpresionAritmetica:ea
                                    {: RESULT = ea; :}
                                    ;
                         
Asignacion                      ::= IDENTIFICADOR:id DeclaracionSimple:ds
                                    {: RESULT = new Node("asig",new Node(id), ds); :}
                                    |
                                    ExpresionIncDec;

Type                            ::= INT               
                                    |
                                    DOUBLE              
                                    |
                                    CHAR
                                    |
                                    BOOLEAN
                                    |
                                    STRING ; 

/*LLAMADOS*/
Llamado                         ::= Print
                                    |
                                    LlamadoMetodos
                                    ;

LlamadoMetodos                  ::= IDENTIFICADOR Contenido ;
Contenido                       ::= PARIZQ  SubContenido PARDER;
SubContenido                    ::= IDENTIFICADOR T
                                    | ;

T                               ::= COMA IDENTIFICADOR  T
                                    | ;
   
/*PARAMETROD*/                     
Parametros                      ::= Type IDENTIFICADOR:id Multiple:mult
                                    {: RESULT = new Node("params", new Node(id), mult); :}
                                    |
                                    Type IDENTIFICADOR:id
                                    {: RESULT = new Node(id); :}
                                    | ;

Multiple                        ::= COMA Parametros:param
                                    {: RESULT = param; :}
                                    ;

Print                           ::= PRINT PARIZQ x PARDER;

x                               ::= AsigValor
                                    |
                                    LlamadoMetodos
                                    |  ;
/*FUNCTION */
FunctionList                    ::= FunctionPart:fp FunctionList:fl
                                    {: RESULT = new Node("FUNCTIONS", fl, fp); :}
                                    |
                                    FunctionPart:fp
                                    {: RESULT = fp; :}
                                    ;

FunctionPart                    ::= FUNCTION VOID IDENTIFICADOR:id PARIZQ Parametros:param PARDER Stmts:st
                                    {: RESULT = new Node(id, param, st); :}
                                    |
                                    FUNCTION Type IDENTIFICADOR:id PARIZQ Parametros:param PARDER EndFunction:ef
                                    {: RESULT = new Node(id,param,ef); :}
                                    ;

EndFunction                     ::= DO BodyList:bdl RETURN Return:rt PUNTOCOMA END
                                    {: RESULT = new Node("branch", bdl, rt);  :}
                                    |
                                    DO RETURN Return:rt PUNTOCOMA END
                                    {: RESULT = new Node("return",rt); :}
                                    ;

Return                          ::= LlamadoMetodos:lm
                                    {: RESULT = lm; :}
                                    |
                                    AsigValor:av
                                    {: RESULT = av; :}
                                    ;

/*BLOQUE*/
Bloque                          ::= Loops:lp
                                    {: RESULT = lp; :}
                                    |
                                    Conditional:cond
                                    {: RESULT = cond; :}
                                    ;

Loops                           ::= ForLoop:flp
                                    {: RESULT = flp; :}
                                    |
                                    WhileLoop:wlp
                                    {: RESULT = wlp; :}
                                    ;

Conditional                     ::= IfConditional:ic
                                    {: RESULT = ic; :}
                                    |
                                    SwitchConditional ;

ForLoop                         ::= FOR PARIZQ ExprFor PARDER Stmts;

ExprFor                         ::= DeclaracionVariable PUNTOCOMA Expresion PUNTOCOMA ExpresionIncDec ;

WhileLoop                       ::= WHILE PARIZQ ExpresionBooleana:exprb PARDER Stmts:st
                                    {: RESULT = new Node("WHILE", exprb, st); :}
                                    ;

IfConditional                   ::= IF ConditionGroup:cg Stmts:st
                                    {: RESULT = new Node("IF",cg, st); :}
                                    |
                                    IF ConditionGroup Stmts ELSE Stmts;

ConditionGroup                  ::= PARIZQ ExpresionBooleana:eb PARDER
                                    {: RESULT = eb; :};

SwitchConditional               ::= SWITCH PARIZQ IDENTIFICADOR PARDER DO ExpresionSwitch END;

ExpresionSwitch                 ::= CASE Switch DOSPUNTOS Stmts BREAK PUNTOCOMA ExpresionSwitch
                                    |
                                    OTHER DOSPUNTOS Stmts BREAK PUNTOCOMA;

Switch                          ::= NUMERO
                                    |
                                    CARACTER;

/*EXPRESIONES*/
Expresion                       ::= ExpresionBooleana
                                    |
                                    ExpresionAritmetica;

ExpresionAritmetica             ::= ExpresionAritmetica:ea SUMA Factor:fct
                                    {: RESULT = new Node("+",ea,fct); :}
                                    |
                                    ExpresionAritmetica:ea MENOS Factor:fct
                                    {: RESULT = new Node("-",ea,fct); :}
                                    |
                                    Factor:fct
                                    {: RESULT = fct; :}
                                    ;

Factor                          ::= Factor:fct MULT Term:trm
                                    {: RESULT = new Node("*",fct,trm); :}
                                    |
                                    Factor:fct DIV Term:trm
                                    {: RESULT = new Node("/",fct,trm); :}
                                    |
                                    Factor:fct MOD Term:trm
                                    {: RESULT = new Node("%",fct,trm); :}
                                    |
                                    Term:trm
                                    {: RESULT = trm; :}
                                    ;

Term                            ::= PARIZQ ExpresionAritmetica:ea PARDER
                                    {: RESULT = ea; :}
                                    |
                                    Valor:vlr
                                    {: RESULT = vlr; :}
                                    ;

ExpresionBooleana               ::= ExpresionComparacion:exprc UnionExpresion:uexpr
                                    {: RESULT = new Node("conditions", exprc, uexpr); :}
                                    |
                                    ExpresionComparacion:ec
                                    {: RESULT = ec; :}
                                    |
                                    Booleana:bool
                                    {: RESULT = bool; :}
                                    ;

UnionExpresion                  ::= OperadorComparacion:opcomp ExpresionComparacion:exprc UnionExpresion:uexpr
                                    {: RESULT = new Node(opcomp.label, exprc, uexpr); :}
                                    |
                                    OperadorComparacion ExpresionComparacion
                                    ;

ExpresionComparacion            ::= AsigValor:av1 OperadorRelacional:oprel AsigValor:av2
                                    {: RESULT = new Node(oprel.label, av1, av2); :}
                                    |
                                    NOT ExpresionComparacion:exprc
                                    {: RESULT = new Node("negation", exprc); :}
                                    ;

OperadorRelacional              ::= MAYOR
                                    {: RESULT = new Node(">"); :}
                                    |
                                    MENOR
                                    {: RESULT = new Node("<"); :}
                                    |
                                    MAYORIGUAL
                                    {: RESULT = new Node(">="); :}
                                    |
                                    MENORIGUAL
                                    {: RESULT = new Node("<="); :}
                                    |
                                    IGUAL
                                    {: RESULT = new Node("="); :}
                                    |
                                    DIFERENTE
                                    {: RESULT = new Node("!="); :}
                                    ;
OperadorComparacion             ::= AND
                                    |
                                    OR
                                    ;
ExpresionIncDec                 ::= IDENTIFICADOR Incremento
                                    |
                                    IDENTIFICADOR Decremento;

Incremento                      ::= SUMA SUMA;
Decremento                      ::= MENOS MENOS;
package com.compiler.hashtag;

import java_cup.runtime.*;
import java.util.*;
import java.io.FileReader;

parser code {:
    public Node root = null;
    public int errors = 0;
    public int fatal = 0;
    //Lexer lexer = (Lexer) this.getScanner();

    public JavaSymbol getCurrentSymbol() {
        return (JavaSymbol) this.cur_token;
    }

    public void report_error(String message, Object info){
        StringBuilder m = new StringBuilder("");
        if(info instanceof java_cup.runtime.Symbol){
            m.append("Error: ");
            m.append(info);
            m.append(" : " + message);
        } else {
            if (info instanceof String) {
                errors++;
                m.append("    "+ errors + "==> " + info + " "+ message+"\n");
            }
        }
        Editor.console.setText(Editor.console.getText() + m.toString() + "\n");
    }

    public void report_fatal_error(String message, Object info){
        fatal++;
        report_error(message, info);
    }
:}

//-----------------------------------------Terminals and non terminals---------------------------------------

terminal SUMA,MENOS,DIV,MULT,MAYOR,MENOR,MAYORIGUAL,MENORIGUAL,NOT,DIFERENTE,IGUAL,ASIGNACION;
terminal PARDER,PARIZQ,MOD,COMA,PUNTOCOMA,MAINBEGIN,DOSPUNTOS;
terminal AND,OR,FOR,IF,ELSE,WHILE,BEGIN,END,SWITCH,CASE,DO,FUNCTION,BREAK,RETURN,PRINT;
terminal INT,DOUBLE,CHAR,STRING,BOOLEAN,READSTRING,READCHAR,READINT,READDOUBLE,OTHER,VOID;

terminal Integer   NUMERO;
terminal Double    REAL;
terminal Character CARACTER;
terminal String    CADENA,IDENTIFICADOR;
terminal Boolean   FALSE,TRUE;


non terminal Node Main,BodyList,BodyPart,Type,Asignacion,AsigValor,Program,FunctionList,FunctionPart,Stmts;
non terminal Node Instruccion,Llamado,ExpresionComparacion,LogicalOper,RelationalOper,ExpresionIncDec,Incremento,Decremento,BooleanExprList,BooleanExprPart;
non terminal Node Bloque,Loops,Conditional,ForLoop,WhileLoop,IfConditional,SwitchConditional,ExprFor,Switch,ExprSwitchList,ExprSwitchPart;
non terminal Node ExpresionBooleana,ExpresionAritmetica,Factor,Term,Conditions,Contenido,PrintParam;
non terminal Node Print,LlamadoMetodos,Boolean,Parametros,Param,Valor,Cadena,Return;
non terminal Node VarDeclare,VarInit,VarDeclarationList,Identifier,Functions,Lectura;


precedence left  SUMA,MENOS;
precedence left  MULT,DIV,MOD;
precedence left  PARIZQ;

//--------------------------------------BNF Grammar------------------------------------------

start with Program;

Program                     ::= BEGIN Functions:f Main:mn END
                                {:
                                    RESULT = new Node("PROG", f, mn);
                                    parser.root = RESULT;
                                :}
                                |
                                BEGIN Main:mn END
                                {:
                                    RESULT = new Node("PROG", mn);
                                    parser.root = RESULT;
                                :}
                                |
                                error {: parser.report_error("syntax. Missing token 'begin'.","WRONG"); :} Main END
                                ;

Functions                   ::= FunctionList:fl
                                {: RESULT = new Node("FUNCTIONS",fl); :}
                                ;

Main                        ::= MAINBEGIN Stmts:st
                                {: RESULT = new Node("MAIN",st); :}
                                |
                                error {: parser.report_error("token 'mainbegin'.","MISSING"); :} Stmts;

Stmts                       ::= DO BodyList:bdl END
                                {: RESULT = bdl; :}
                                |
                                DO END
                                {: RESULT = new Node("?"); :}
                                |
                                error {: parser.report_error("block declaration syntax. Unmatched 'do' 'end' tokens.","WRONG"); :} BodyList END
                                ;

BodyList                    ::= BodyPart:bp BodyList:bdl
                                {: RESULT = bdl.add(bp); :}
                                |
                                BodyPart:bp
                                {: RESULT = new Node("body", bp); :}
                                ;

BodyPart                    ::= Instruccion:ins PUNTOCOMA
                                {: RESULT = ins; :}
                                |
                                Bloque:bq
                                {: RESULT = bq; :}
                                |
                                Asignacion:asig PUNTOCOMA
                                {: RESULT = asig; :}
                                |
                                Return:ret PUNTOCOMA
                                {: RESULT = ret; :}
                                |
                                error {: parser.report_error("start of expression.","ILLEGAL"); :} BodyPart
                                ;


Instruccion                 ::= VarDeclare:vd
                                {: RESULT = vd; :}
                                |
                                VarInit:vi
                                {: RESULT = vi; :}
                                |
                                Llamado:l
                                {: RESULT = l; :}
                                |
                                error {: parser.report_error("variable initialization, declaration or function call","WRONG"); :} PUNTOCOMA
                                ;

Lectura                     ::= READINT PARIZQ PARDER
                                {: RESULT = new Node("READINT"); :}
                                |
                                READDOUBLE PARIZQ PARDER
                                {: RESULT = new Node("READDOUBLE"); :}
                                |
                                READSTRING PARIZQ PARDER
                                {: RESULT = new Node("READSTRING"); :}
                                |
                                READCHAR PARIZQ PARDER
                                {: RESULT = new Node("READCHAR"); :}
                                ;

                /*
--------------------------------------------------------------------------------
                S-Atribuida ejemplo (como debería de ser):
                    V -> L
                    T -> int | double | char | string | boolean
                    L -> L,P
                    L -> TP
                    P -> id

                    donde:
                    V = VarDeclare
                    L = VarDeclarationList
                    P = Identifier
                    T = Type

                    Identifier not necessary anymore?
--------------------------------------------------------------------------------
                */

VarDeclare                  ::= VarDeclarationList:vdl
                                {:
                                    RESULT = vdl;
                                :}
                                ;

VarDeclarationList          ::= VarDeclarationList:vdl COMA Identifier:iden
                                {:
                                    iden.getData().setType(vdl.getData().getType());
                                    RESULT = vdl.add(iden);
                                :}
                                |
                                Type:tp Identifier:iden
                                {:
                                    iden.getData().setType(tp.label); //habria que hacer clase Tipo propia?
                                    RESULT = new Node("declare", iden);
                                    RESULT.getData().setType(tp.label); //temporal solution so that every child of "declare" updates their type
                                :}
                                ;

Identifier                  ::= IDENTIFICADOR:id
                                {:
                                    JavaSymbol symbol = (JavaSymbol)CUP$Parser$stack.peek();

                                    Data dat = new Data(symbol.getLexeme(), "IDENTIFIER", null, symbol.getLine(), symbol.getColumn());
                                    RESULT = new Node(dat, id);
                                :}
                                ;

VarInit                     ::= VarDeclare:vd ASIGNACION AsigValor:av
                                {:
                                    for (Node declared: vd.getChildren()) {
                                        declared.getData().setValue(av.getData().getValue());
                                    }
                                    RESULT = new Node("init",vd,av);
                                :}
                                |
                                VarDeclare:vd ASIGNACION Lectura:lec
                                {: RESULT = new Node("init",vd,lec); :}
                                ;

AsigValor                   ::= Cadena:c
                                {: RESULT = c; :}
                                |
                                CARACTER:ch
                                {:
                                    JavaSymbol symbol = (JavaSymbol)CUP$Parser$stack.peek();

                                    Data dat = new Data(symbol.getLexeme(), "CHARACTER", ch, symbol.getLine(), symbol.getColumn());
                                    RESULT = new Node(dat, ("'").concat(Character.toString(ch)).concat("'"));
                                :}
                                |
                                Boolean:bool
                                {:
                                    RESULT = bool;
                                :}
                                ;

Boolean                     ::= TRUE
                                {:
                                    JavaSymbol symbol = (JavaSymbol)CUP$Parser$stack.peek();

                                    Data dat = new Data(symbol.getLexeme(), "TRUE", new Boolean(true), symbol.getLine(), symbol.getColumn());
                                    RESULT = new Node(dat, "true");
                                :}
                                |
                                FALSE
                                {:
                                    JavaSymbol symbol = (JavaSymbol)CUP$Parser$stack.peek();

                                    Data dat = new Data(symbol.getLexeme(), "FALSE", new Boolean(false), symbol.getLine(), symbol.getColumn());
                                    RESULT = new Node(dat, "false");
                                :}
                                ;

Cadena                      ::= CADENA:cad
                                {:
                                    JavaSymbol symbol = (JavaSymbol)CUP$Parser$stack.peek();

                                    Data dat = new Data(symbol.getLexeme(), "STRING", cad, symbol.getLine(), symbol.getColumn());
                                    RESULT = new Node(dat,"\""+cad+"\"");
                                :}
                                |
                                ExpresionAritmetica:ea
                                {: RESULT = ea; :}
                                ;

Asignacion                  ::= Identifier:iden ASIGNACION AsigValor:av
                                {: RESULT = new Node("assign",iden, av); :}
                                |
                                Identifier:iden ASIGNACION Lectura:lec
                                {: RESULT = new Node("assign",iden, lec); :}
                                |
                                ExpresionIncDec:update
                                {: RESULT = update; :}
                                ;

Type                        ::= INT
                                {: RESULT = new Node("int"); :}
                                |
                                DOUBLE
                                {: RESULT = new Node("double"); :}
                                |
                                CHAR
                                {: RESULT = new Node("char"); :}
                                |
                                BOOLEAN
                                {: RESULT = new Node("boolean"); :}
                                |
                                STRING
                                {: RESULT = new Node("string"); :}
                                ;

//--------------------------Method calls-------------------------------

Llamado                     ::= Print:pr
                                {: RESULT = pr; :}
                                |
                                LlamadoMetodos:lm
                                {: RESULT = lm; :}
                                ;

LlamadoMetodos              ::= Identifier:id PARIZQ Contenido:cont PARDER
                                {: RESULT = new Node("function_call",id,cont); :}
                                |
                                Identifier:id PARIZQ PARDER
                                {: RESULT = new Node("function_call", id, new Node("params",new Node("?"))); :}
                                ;

Contenido                   ::= AsigValor:av COMA Contenido:cont
                                {: RESULT = cont.add(av); :}
                                |
                                AsigValor:av
                                {: RESULT = new Node("params",av); :};

//--------------------------Parameters-------------------------------

Parametros                  ::= Parametros:ps COMA Param:p
                                {: RESULT = ps.add(p); :}
                                |
                                Param:p
                                {: RESULT = new Node("parameters",p); :}
                                |
                                {: RESULT = new Node("?"); :}
                                ;

Param                       ::= Type:tp Identifier:id
                                {:
                                    RESULT = id;
                                    RESULT.getData().setType(tp.label);
                                :}
                                ;

Print                       ::= PRINT PARIZQ PrintParam:pp PARDER
                                {: RESULT = new Node("PRINT", pp); :};

PrintParam                  ::= Cadena:cad
                                {: RESULT = cad; :}
                                |
                                LlamadoMetodos:lm
                                {: RESULT = lm; :}
                                |
                                {: RESULT = new Node("?"); :}
                                ;

//--------------------------Functions-------------------------------

FunctionList                ::= FunctionPart:fp FunctionList:fl
                                {: RESULT = fl.add(fp); :}
                                |
                                FunctionPart:fp
                                {: RESULT = new Node("body",fp); :};

FunctionPart                ::= FUNCTION VOID IDENTIFICADOR:id PARIZQ Parametros:param PARDER Stmts:st
                                {: RESULT = new Node(id, param, st); :}
                                |
                                FUNCTION Type IDENTIFICADOR:id PARIZQ Parametros:param PARDER Stmts:st
                                {: RESULT = new Node(id, param, st); :}
                                |
                                FUNCTION error {:
                                            parser.report_error("function declaration. Usage example: function <type> <id> () do ... end","WRONG");
                                        :} Stmts
                                ;

Return                      ::= RETURN LlamadoMetodos:lm
                                {: RESULT = new Node("RETURN",lm); :}
                                |
                                RETURN AsigValor:av
                                {: RESULT = new Node("RETURN", av); :}
                                ;

//--------------------------Blocks (if, switch, while, for)-------------------------------

Bloque                      ::= Loops:lp
                                {: RESULT = lp; :}
                                |
                                Conditional:cond
                                {: RESULT = cond; :}
                                ;

Loops                       ::= ForLoop:flp
                                {: RESULT = flp; :}
                                |
                                WhileLoop:wlp
                                {: RESULT = wlp; :}
                                ;

Conditional                 ::= IfConditional:ic
                                {: RESULT = ic; :}
                                |
                                SwitchConditional:swc
                                {: RESULT = swc; :};

ForLoop                     ::= FOR PARIZQ ExprFor:expr PARDER Stmts:st
                                {: RESULT = new Node("FOR", expr, st); :}
                                |
                                FOR error {: parser.report_error("FOR statement declaration","WRONG"); :} Stmts;

ExprFor                     ::= INT Asignacion:asig PUNTOCOMA ExpresionBooleana:exprb PUNTOCOMA ExpresionIncDec:update
                                {: RESULT = new Node("structure",asig,exprb,update); :};

WhileLoop                   ::= WHILE PARIZQ Conditions:cond PARDER Stmts:st
                                {: RESULT = new Node("WHILE", cond, st); :}
                                |
                                WHILE PARIZQ PARDER error {: parser.report_error("WHILE statement declaration, missing boolean expression.","WRONG"); :} Stmts
                                |
                                WHILE error {: parser.report_error("WHILE statement declaration. Usage example:\n\n\twhile (<boolean expression>) do \n\t\t...\n\tend","WRONG"); :} Stmts
                                ;

IfConditional               ::= IF PARIZQ Conditions:cond PARDER Stmts:st
                                {: RESULT = new Node("IF",cond, st); :}
                                |
                                IF PARIZQ Conditions:cond PARDER Stmts:st1 ELSE Stmts:st2
                                {: RESULT = new Node("IF",cond,st1,new Node("ELSE",st2)); :}
                                |
                                IF error {: parser.report_error("IF statement declaration. Usage example:\n\n\tif (<boolean expression>) do \n\t\t...\n\tend", "WRONG"); :} Stmts;

Conditions                  ::= ExpresionBooleana:eb
                                {: RESULT = eb; :}
                                |
                                Identifier:id
                                {: RESULT = id; :};

SwitchConditional           ::= SWITCH PARIZQ Identifier:id PARDER DO ExprSwitchList:exprl END
                                {: RESULT = new Node("SWITCH", id, exprl); :}
                                |
                                SWITCH PARIZQ Switch:sw PARDER DO ExprSwitchList:exprl END
                                {: RESULT = new Node("SWITCH", sw, exprl); :}
                                |
                                SWITCH error {:parser.report_error("SWITCH statement declaration.","WRONG"); :} ExprSwitchList
                                ;

ExprSwitchList              ::= ExprSwitchPart:exprp ExprSwitchList:exprl
                                {: RESULT = exprl.add(exprp); :}
                                |
                                ExprSwitchPart:exprp
                                {: RESULT = new Node("body",exprp); :};

ExprSwitchPart              ::= CASE Switch:sw DOSPUNTOS Stmts:st BREAK PUNTOCOMA
                                {: RESULT = new Node("CASE", sw, st); :}
                                |
                                OTHER DOSPUNTOS Stmts:st BREAK PUNTOCOMA
                                {: RESULT = new Node("OTHER", st); :}
                                |
                                CASE error {:parser.report_error("CASE syntax in SWITCH statement.","WRONG");:} Stmts;

Switch                      ::= NUMERO:n
                                {: RESULT = new Node(Integer.toString(n)); :}
                                |
                                CARACTER:c
                                {: RESULT = new Node(Character.toString(c)); :};

//--------------------------Arithmetic expressions-------------------------------

ExpresionAritmetica         ::= ExpresionAritmetica:ea SUMA Factor:fct
                                {: RESULT = new Node("+",ea,fct); :}
                                |
                                ExpresionAritmetica:ea MENOS Factor:fct
                                {: RESULT = new Node("-",ea,fct); :}
                                |
                                Factor:fct
                                {: RESULT = fct; :}
                                ;

Factor                      ::= Factor:fct MULT Term:trm
                                {: RESULT = new Node("*",fct,trm); :}
                                |
                                Factor:fct DIV Term:trm
                                {: RESULT = new Node("/",fct,trm); :}
                                |
                                Factor:fct MOD Term:trm
                                {: RESULT = new Node("%",fct,trm); :}
                                |
                                Term:trm
                                {: RESULT = trm; :}
                                ;

Term                        ::= PARIZQ ExpresionAritmetica:ea PARDER
                                {: RESULT = ea; :}
                                |
                                Valor:vlr
                                {: RESULT = vlr; :}
                                ;

Valor                       ::= NUMERO:num
                                {:
                                    JavaSymbol symbol = (JavaSymbol)CUP$Parser$stack.peek();

                                    Data dat = new Data(symbol.getLexeme(), "NUMBER", num, symbol.getLine(), symbol.getColumn());
                                    RESULT = new Node(dat, Integer.toString(num));
                                :}
                                |
                                Identifier:id
                                {: RESULT = id; :}
                                |
                                REAL:r
                                {:
                                    JavaSymbol symbol = (JavaSymbol)CUP$Parser$stack.peek();

                                    Data dat = new Data(symbol.getLexeme(), "REAL", r, symbol.getLine(), symbol.getColumn());
                                    RESULT = new Node(dat, Double.toString(r));
                                :}
                                ;

//--------------------------Boolean expressions-------------------------------

ExpresionBooleana           ::= BooleanExprList:bel
                                {: RESULT = bel; :}
                                |
                                Boolean:bool
                                {: RESULT = bool; :}
                                ;

BooleanExprList             ::= BooleanExprList:list LogicalOper BooleanExprPart:part
                                {: RESULT = list.add(part); :}
                                |
                                BooleanExprPart:part
                                {: RESULT = new Node("conditions",part); :}
                                ;

BooleanExprPart             ::= ExpresionComparacion:ec
                                {: RESULT = ec; :}
                                ;

ExpresionComparacion        ::= AsigValor:v1 RelationalOper:oprel AsigValor:v2
                                {: RESULT = new Node(oprel.label, v1, v2); :}
                                |
                                NOT ExpresionComparacion:exprc
                                {: RESULT = new Node("negation", exprc); :}
                                ;

RelationalOper              ::= MAYOR
                                {: RESULT = new Node(">"); :}
                                |
                                MENOR
                                {: RESULT = new Node("<"); :}
                                |
                                MAYORIGUAL
                                {: RESULT = new Node(">="); :}
                                |
                                MENORIGUAL
                                {: RESULT = new Node("<="); :}
                                |
                                IGUAL
                                {: RESULT = new Node("="); :}
                                |
                                DIFERENTE
                                {: RESULT = new Node("!="); :}
                                ;

LogicalOper                 ::= AND
                                |
                                OR
                                ;

ExpresionIncDec             ::= Identifier:id Incremento
                                {: RESULT = new Node("increase", id); :}
                                |
                                Identifier:id Decremento
                                {: RESULT = new Node("decrease", id); :};

Incremento                  ::= SUMA SUMA;
Decremento                  ::= MENOS MENOS;